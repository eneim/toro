buildscript {
  repositories {
    maven { url 'https://maven.fabric.io/public' }
  }

  dependencies {
    classpath 'io.fabric.tools:gradle:1.22.0'
  }
}

apply plugin: 'com.android.application'
apply plugin: 'io.fabric'
apply plugin: 'com.getkeepsafe.dexcount'

repositories {
  maven { url 'https://maven.fabric.io/public' }
}

import java.text.DateFormat
import java.text.SimpleDateFormat

def dateTimeInt() {
  DateFormat df = new SimpleDateFormat("yyyyMMdd");
  return Integer.parseInt(df.format(new Date()));
}

def dateTimeString() {
  DateFormat df = new SimpleDateFormat("yyyy.MM.dd");
  return df.format(new Date());
}

android {
  compileSdkVersion rootProject.ext.androidVersion
  buildToolsVersion rootProject.ext.buildToolVersion

  signingConfigs {
    release {
      keyAlias toro_prodKeyAlias
      keyPassword toro_prodKeyPassword
      storeFile file(toro_prodStoreFile)
      storePassword toro_prodStorePassword
    }

    debug {
      keyAlias "debug"
      keyPassword "android"
      storeFile file("debug.jks")
      storePassword "android"
    }
  }

  defaultConfig {
    applicationId "${TORO_SAMPLE_APP_ID}"
    minSdkVersion rootProject.ext.minSupportSDKVersion
    targetSdkVersion rootProject.ext.androidVersion

    versionCode dateTimeInt()
    versionName dateTimeString()
    archivesBaseName = "toro_sample"

    manifestPlaceholders = [fabric_api_key : "${TORO_SAMPLE_FABRIC_API_KEY}",
                            youtube_api_key: "${TORO_SAMPLE_YOUTUBE_API_KEY}"]

    vectorDrawables.useSupportLibrary = true

    // multiDexEnabled true
    testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
  }

  buildTypes {
    release {
      signingConfig signingConfigs.release
      minifyEnabled true
      proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
    }

    debug {
      signingConfig signingConfigs.debug
      applicationIdSuffix ".debug"
      minifyEnabled false
      proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
    }
  }

  packagingOptions {
    exclude 'META-INF/services/javax.annotation.processing.Processor'
  }

  lintOptions {
    disable 'InvalidPackage'
    abortOnError false
  }
}

dependencies {
  compile fileTree(include: ['*.jar'], dir: 'libs')

  compile "com.android.support:design:${rootProject.ext.supportLibraryVersion}"
  compile "com.android.support:appcompat-v7:${rootProject.ext.supportLibraryVersion}"
  compile "com.android.support:cardview-v7:${rootProject.ext.supportLibraryVersion}"

  // compile project(path: ':toro-ext-exoplayer')
  compile project(path: ':toro-extended')
  debugCompile project(path: ':toro-extended')
  compile project(path: ':toro-ext-mediaplayer')
  // compile "im.ene.toro2:toro-extended:2.1.0-SNAPSHOT"

  compile ('com.azoft.carousellayoutmanager:carousel:1.2.1', {
    exclude group: 'com.android.support', module: 'recyclerview-v7'
  })

  compile 'com.github.bumptech.glide:glide:3.7.0'
  compile 'com.squareup.picasso:picasso:2.5.2'

  compile 'com.jakewharton:butterknife:8.5.1'
  annotationProcessor 'com.jakewharton:butterknife-compiler:8.5.1'

  compile('com.crashlytics.sdk.android:crashlytics:2.6.5@aar') {
    transitive = true;
  }

  compile('com.crashlytics.sdk.android:crashlytics-ndk:1.1.5') {
    transitive = true;
  }

  debugCompile 'com.squareup.leakcanary:leakcanary-android:1.5.1'
  releaseCompile 'com.squareup.leakcanary:leakcanary-android-no-op:1.5.1'
  testCompile 'com.squareup.leakcanary:leakcanary-android-no-op:1.5.1'

  // Dependencies for local unit tests
  testCompile "junit:junit:$rootProject.ext.junitVersion"
  testCompile "org.mockito:mockito-all:$rootProject.ext.mockitoVersion"
  testCompile "org.hamcrest:hamcrest-all:$rootProject.ext.hamcrestVersion"

  // Android Testing Support Library's runner and rules
  androidTestCompile "com.android.support.test:runner:$rootProject.ext.runnerVersion"
  androidTestCompile "com.android.support.test:rules:$rootProject.ext.runnerVersion"

  // Dependencies for Android unit tests
  androidTestCompile "junit:junit:$rootProject.ext.junitVersion"
  androidTestCompile "org.mockito:mockito-core:$rootProject.ext.mockitoVersion"
  androidTestCompile 'com.google.dexmaker:dexmaker:1.2'
  androidTestCompile 'com.google.dexmaker:dexmaker-mockito:1.2'

  // Espresso UI Testing
  androidTestCompile "com.android.support.test.espresso:espresso-core:$rootProject.espressoVersion"
  androidTestCompile "com.android.support.test.espresso:espresso-contrib:$rootProject.espressoVersion"
  androidTestCompile "com.android.support.test.espresso:espresso-intents:$rootProject.espressoVersion"

  // Resolve conflicts between main and test APK:
  androidTestCompile "com.android.support:support-annotations:$rootProject.supportLibraryVersion"
  androidTestCompile "com.android.support:support-v4:$rootProject.supportLibraryVersion"
  androidTestCompile "com.android.support:recyclerview-v7:$rootProject.supportLibraryVersion"
  androidTestCompile "com.android.support:appcompat-v7:$rootProject.supportLibraryVersion"
  androidTestCompile "com.android.support:design:$rootProject.supportLibraryVersion"
}

crashlytics {
  enableNdk true
  androidNdkOut 'src/main/obj'
  androidNdkLibsOut 'src/main/libs'
}

// apply plugin: 'com.google.gms.google-services'
